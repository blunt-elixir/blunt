defmodule Blunt.Message.Changeset do
  @moduledoc false

  alias Ecto.Changeset
  alias Blunt.Message.{Input, Metadata, State}
  alias Blunt.Message.Schema.BuiltInValidations
  alias Blunt.Message.Changeset, as: MessageChangeset

  def generate do
    quote location: :keep, generated: true do
      @doc false
      def changeset(values, opts \\ []) when is_list(values) or is_map(values) or is_struct(values) do
        MessageChangeset.changeset(%__MODULE__{}, values, opts)
      end
    end
  end

  @type message :: atom()
  @type discarded_data :: map()
  @type changeset :: Ecto.Changeset.t()
  @type values :: maybe_improper_list | map | struct

  @spec changeset(message(), values(), keyword()) :: {changeset, discarded_data} | changeset

  def changeset(message, values, opts \\ [])

  def changeset(message, values, opts) when is_struct(values),
    do: changeset(message, Map.from_struct(values), opts)

  def changeset(%{__struct__: message}, values, opts) when is_list(values) or is_map(values),
    do: changeset(message, values, opts)

  def changeset(message_module, values, opts) when is_list(values) or is_map(values) do
    fields = Metadata.field_names(message_module)
    virtual_fields = Metadata.field_names(message_module, :virtual)
    required_fields = Metadata.field_names(message_module, :required)
    static_fields = Metadata.field_names(message_module, :static) |> Enum.map(&to_string/1)

    blunt_id = UUID.uuid4()

    final_values =
      values
      |> Map.put(:__blunt_id, blunt_id)
      |> Input.normalize(message_module)
      |> set_defaults_for_required_fields(message_module)
      |> autogenerate_fields(message_module)
      |> message_module.before_validate()
      |> Enum.reject(fn {name, _value} -> name in static_fields end)
      |> Enum.into(%{})

    embeds = message_module.__schema__(:embeds)

    discarded_data =
      final_values
      |> Map.drop(Enum.map(fields, &to_string/1))
      |> Map.drop(Enum.map(embeds, &to_string/1))

    user_supplied_fields =
      values
      |> Map.take(Enum.map(fields, &to_string/1))
      |> Map.keys()
      |> Enum.map(&String.to_existing_atom/1)

    _ = State.put(blunt_id, %{discarded_data: discarded_data, user_supplied_fields: user_supplied_fields})

    changeset =
      message_module
      |> struct()
      |> Changeset.cast(final_values, (fields ++ virtual_fields) -- embeds)

    opts = opts |> List.wrap() |> Keyword.new()
    embed_changeset = {__MODULE__, :changeset, []}

    embeds
    |> Enum.reduce(changeset, &Changeset.cast_embed(&2, &1, with: embed_changeset))
    |> Changeset.validate_required(required_fields)
    |> run_built_in_validations(message_module)
    |> message_module.handle_validate(opts)
  end

  defp set_defaults_for_required_fields(values, message_module) do
    required_fields =
      message_module
      |> Metadata.fields(:required)
      |> Enum.map(fn {name, _, opts} -> {to_string(name), Keyword.get(opts, :default)} end)
      |> Enum.reject(&(elem(&1, 1) == nil))
      |> Enum.into(%{})

    Map.merge(values, required_fields, fn
      _, nil, default_value -> default_value
      _, incoming_value, _default_value -> incoming_value
    end)
  end

  defp run_built_in_validations(changeset, message) do
    message
    |> Metadata.built_in_validations()
    |> Enum.reduce(changeset, &BuiltInValidations.run/2)
  end

  defp autogenerate_fields(values, message) do
    message
    |> Metadata.autogenerated_fields()
    |> Enum.into(%{}, &run_autogenerator(&1, message))
    |> Map.merge(values)
  end

  defp run_autogenerator({name, {m, f}}, message),
    do: run_autogenerator({name, {m, f, []}}, message)

  defp run_autogenerator({name, {m, f, a}}, message) do
    Code.ensure_compiled!(m)

    unless function_exported?(m, f, length(a)) do
      raise Blunt.Message.Error,
        message: "#{inspect(message)}.#{name} autogenerate function '#{inspect(m)}.#{f}/#{length(a)}' not found."
    end

    {to_string(name), apply(m, f, a)}
  end

  def format_errors(changeset) do
    Changeset.traverse_errors(changeset, fn {message, opts} ->
      Regex.replace(~r"%{(\w+)}", message, fn _, key ->
        opts |> Keyword.get(String.to_existing_atom(key), key) |> to_string()
      end)
    end)
  end
end
